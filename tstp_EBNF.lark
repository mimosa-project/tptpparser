%ignore COMMENT
%ignore /[ \t\f\r\n]+/

tptp_root            : tptp_input*
tptp_input           : annotated_formula | include

annotated_formula    : thf_annotated | tff_annotated | tcf_annotated | fof_annotated | cnf_annotated | tpi_annotated

tpi_annotated        : TPI "(" NAME "," FORMULA_ROLE "," tpi_formula annotations ")" "."
tpi_formula          : fof_formula
thf_annotated        : THF "(" NAME "," FORMULA_ROLE "," thf_formula annotations ")" "."
tff_annotated        : TFF "(" NAME "," FORMULA_ROLE "," tff_formula annotations ")" "."
tcf_annotated        : TCF "(" NAME "," FORMULA_ROLE "," tcf_formula annotations ")" "."
fof_annotated        : FOF "(" NAME "," FORMULA_ROLE "," fof_formula annotations ")" "."
cnf_annotated        : CNF "(" NAME "," FORMULA_ROLE "," cnf_formula annotations ")" "."
annotations          :  "," source optional_info | null

FORMULA_ROLE         : LOWER_WORD
//FORMULA_ROLE         : axiom | hypothesis | definition | assumption | lemma | theorem | corollary | conjecture |  negated_conjecture | plain | type | fi_domain | fi_functors | fi_predicates | unknown

thf_formula          : thf_logic_formula | thf_atom_typing | thf_subtype | thf_sequent | LOWER_WORD ":" thf_quantified_formula//LOWER_WORD ":" thf_quantified_formula追加
thf_logic_formula    : thf_unitary_formula | thf_unary_formula | thf_binary_formula | thf_defined_infix
thf_binary_formula   : thf_binary_nonassoc | thf_binary_assoc | thf_binary_type

thf_binary_nonassoc  : thf_unit_formula NONASSOC_CONNECTIVE thf_unit_formula
thf_binary_assoc     : thf_or_formula | thf_and_formula | thf_apply_formula
thf_or_formula       : thf_unit_formula VLINE thf_unit_formula | thf_or_formula VLINE thf_unit_formula
thf_and_formula      : thf_unit_formula AND_CONNECTIVE thf_unit_formula | thf_and_formula AND_CONNECTIVE thf_unit_formula

thf_apply_formula    : thf_unit_formula APPLY_SYMBOL thf_unit_formula | thf_apply_formula APPLY_SYMBOL thf_unit_formula
thf_unit_formula     : thf_unitary_formula | thf_unary_formula | thf_defined_infix
thf_preunit_formula  : thf_unitary_formula | thf_prefix_unary
thf_unitary_formula  : thf_quantified_formula | thf_atomic_formula | VARIABLE | "(" thf_logic_formula ")"

thf_quantified_formula : thf_quantification thf_unit_formula
thf_quantification   : THF_QUANTIFIER "[" thf_variable_list "]" ":"
thf_variable_list    : thf_typed_variable | thf_typed_variable "," thf_variable_list
thf_typed_variable   : VARIABLE ":" thf_top_level_type | VARIABLE //VARIABLE追加
thf_unary_formula    : thf_prefix_unary | thf_infix_unary
thf_prefix_unary     : UNARY_CONNECTIVE thf_preunit_formula
thf_infix_unary      : thf_unitary_term INFIX_INEQUALITY thf_unitary_term
thf_atomic_formula   : thf_plain_atomic | thf_defined_atomic | thf_system_atomic | thf_fof_function
thf_plain_atomic     : constant | thf_tuple 

thf_defined_atomic   : defined_constant | thf_conditional | thf_let | "(" thf_conn_term ")" | thf_defined_term
thf_defined_term     : defined_term | TH1_DEFINED_TERM
thf_defined_infix    : thf_unitary_term INFIX_EQUALITY thf_unitary_term

thf_system_atomic    : system_constant

thf_fof_function     : FUNCTOR "(" thf_arguments ")" | DEFINED_FUNCTOR "(" thf_arguments ")" | SYSTEM_FUNCTOR "(" thf_arguments ")"

thf_conditional      : DOLLAR_ITE "(" thf_logic_formula "," thf_logic_formula "," thf_logic_formula ")"
thf_let              : DOLLAR_LET "(" thf_let_types "," thf_let_defns "," thf_formula ")"
thf_let_types        : thf_atom_typing | "[" thf_atom_typing_list "]"
thf_atom_typing_list : thf_atom_typing | thf_atom_typing "," thf_atom_typing_list
thf_let_defns        : thf_let_defn | "[" thf_let_defn_list "]"
thf_let_defn         : thf_logic_formula ASSIGNMENT thf_logic_formula
thf_let_defn_list    : thf_let_defn | thf_let_defn "," thf_let_defn_list

thf_unitary_term     : thf_atomic_formula | VARIABLE | "(" thf_logic_formula ")"
thf_tuple            : "[]" | "[" thf_formula_list "]"
thf_formula_list     : thf_logic_formula | thf_logic_formula "," thf_formula_list
thf_conn_term        : NONASSOC_CONNECTIVE | ASSOC_CONNECTIVE |INFIX_EQUALITY | INFIX_INEQUALITY | UNARY_CONNECTIVE

thf_arguments        : thf_formula_list

thf_atom_typing      : untyped_atom ":" thf_top_level_type | "(" thf_atom_typing ")" | ARROW ":" thf_top_level_type //ARROW ":" thf_top_level_typeを追加
thf_top_level_type   : thf_unitary_type | thf_mapping_type | thf_apply_type

thf_unitary_type     : thf_unitary_formula
//thf_unitary_type     : thf_atomic_type | th1_quantified_type
//thf_atomic_type      : TYPE_CONSTANT | DEFINED_TYPE | VARIABLE | thf_mapping_type | "(" thf_atomic_type ")"
//th1_quantified_type  : "!>" "[" thf_variable_list "]" ":" thf_unitary_type

thf_apply_type       : thf_apply_formula
thf_binary_type      : thf_mapping_type | thf_xprod_type | thf_union_type 

thf_mapping_type     : thf_unitary_type ARROW thf_unitary_type | thf_unitary_type ARROW thf_mapping_type

thf_xprod_type       : thf_unitary_type STAR thf_unitary_type | thf_xprod_type STAR thf_unitary_type

thf_union_type       : thf_unitary_type PLUS thf_unitary_type | thf_union_type PLUS thf_unitary_type

thf_subtype          : untyped_atom SUBTYPE_SIGN atom

thf_sequent          : thf_tuple GENTZEN_ARROW thf_tuple | "(" thf_sequent ")"

//logic_defn_rule      : logic_defn_lhs ASSIGNMENT logic_defn_rhs
//logic_defn_lhs       : logic_defn_value | thf_top_level_type  | NAME
//logic_defn_lhs       : "$"constants | "$"quantification | "$"consequence | "$"modalities

//logic_defn_rhs       : logic_defn_value | thf_unitary_formula
//logic_defn_value     : DEFINED_CONSTANT
//logic_defn_value     : "$"rigid | "$"flexible | "$"CONSTANT | "$"varying | "$"cumulative | "$"decreasing | "$"local | "$"global | "$"modal_system_k | "$"modal_system_t | "$"modal_system_d 
//                        | "$"modal_system_s4 | "$"modal_system_s5 | "$"modal_axiom_k | "$"modal_axiom_t | "$"modal_axiom_b | "$"modal_axiom_d | "$"modal_axiom_4 | "$"modal_axiom_5

tff_formula          : tff_logic_formula | tff_atom_typing | tff_subtype | tfx_sequent
tff_logic_formula    : tff_unitary_formula | tff_unary_formula | tff_binary_formula | tff_defined_infix

tff_binary_formula   : tff_binary_nonassoc | tff_binary_assoc
tff_binary_nonassoc  : tff_unit_formula NONASSOC_CONNECTIVE tff_unit_formula
tff_binary_assoc     : tff_or_formula | tff_and_formula
tff_or_formula       : tff_unit_formula VLINE tff_unit_formula | tff_or_formula VLINE tff_unit_formula
tff_and_formula      : tff_unit_formula AND_CONNECTIVE tff_unit_formula | tff_and_formula AND_CONNECTIVE tff_unit_formula
tff_unit_formula     : tff_unitary_formula | tff_unary_formula | tff_defined_infix
tff_preunit_formula  : tff_unitary_formula | tff_prefix_unary
tff_unitary_formula  : tff_quantified_formula | tff_atomic_formula | tfx_unitary_formula | "(" tff_logic_formula ")"
tfx_unitary_formula  : VARIABLE
tff_quantified_formula : FOF_QUANTIFIER "[" tff_variable_list "]" ":" tff_unit_formula

tff_variable_list    : tff_variable | tff_variable "," tff_variable_list
tff_variable         : tff_typed_variable | VARIABLE
tff_typed_variable   : VARIABLE ":" tff_atomic_type
tff_unary_formula    : tff_prefix_unary | tff_infix_unary

tff_prefix_unary     : UNARY_CONNECTIVE tff_preunit_formula
tff_infix_unary      : tff_unitary_term INFIX_INEQUALITY tff_unitary_term

tff_atomic_formula   : tff_plain_atomic | tff_defined_atomic | tff_system_atomic
tff_plain_atomic     : constant | FUNCTOR "(" tff_arguments ")"
//tff_plain_atomic     : PROPOSITION | PREDICATE"("tff_arguments")"
tff_defined_atomic   : tff_defined_plain 

tff_defined_plain    : defined_constant | DEFINED_FUNCTOR "(" tff_arguments ")" | tfx_conditional | tfx_let
//tff_defined_plain    : DEFINED_PROPOSITION | DEFINED_PREDICATE"("tff_arguments")" | tfx_conditional | tfx_let

tff_defined_infix    : tff_unitary_term INFIX_EQUALITY tff_unitary_term
tff_system_atomic    : system_constant | SYSTEM_FUNCTOR "(" tff_arguments ")"
//tff_system_atomic    : SYSTEM_PROPOSITION |                            SYSTEM_PREDICATE"("tff_arguments")"
tfx_conditional      : DOLLAR_ITE "(" tff_logic_formula "," tff_term "," tff_term ")"
tfx_let              : DOLLAR_LET "(" tfx_let_types "," tfx_let_defns "," tff_term ")"
tfx_let_types        : tff_atom_typing | "[" tff_atom_typing_list "]"
tff_atom_typing_list : tff_atom_typing | tff_atom_typing "," tff_atom_typing_list
tfx_let_defns        : tfx_let_defn | "[" tfx_let_defn_list "]"
tfx_let_defn         : tfx_let_lhs ASSIGNMENT tff_term
tfx_let_lhs          : tff_plain_atomic | tfx_tuple
tfx_let_defn_list    : tfx_let_defn | tfx_let_defn "," tfx_let_defn_list

tff_term             : tff_logic_formula | defined_term | tfx_tuple
tff_unitary_term     : tff_atomic_formula | defined_term | tfx_tuple | VARIABLE | "(" tff_logic_formula ")"
tfx_tuple            : "[]" | "[" tff_arguments "]"

tff_arguments        : tff_term | tff_term "," tff_arguments

tff_atom_typing      : untyped_atom ":" tff_top_level_type | "(" tff_atom_typing ")"
tff_top_level_type   : tff_atomic_type | tff_non_atomic_type
tff_non_atomic_type  : tff_mapping_type | tf1_quantified_type | "(" tff_non_atomic_type ")"
tf1_quantified_type  : TH1_QUANTIFIER "[" tff_variable_list "]" ":" tff_monotype
tff_monotype         : tff_atomic_type | "(" tff_mapping_type ")" | tf1_quantified_type
tff_unitary_type     : tff_atomic_type | "(" tff_xprod_type ")"
tff_atomic_type      : type_constant | DEFINED_TYPE | VARIABLE | TYPE_FUNCTOR "(" tff_type_arguments ")" | "(" tff_atomic_type ")" | tfx_tuple_type 
tff_type_arguments   : tff_atomic_type | tff_atomic_type "," tff_type_arguments
tff_mapping_type     : tff_unitary_type ARROW tff_atomic_type
tff_xprod_type       : tff_unitary_type STAR tff_atomic_type | tff_xprod_type STAR tff_atomic_type

tfx_tuple_type       : "[" tff_type_list "]"
tff_type_list        : tff_top_level_type | tff_top_level_type "," tff_type_list

tff_subtype          : untyped_atom SUBTYPE_SIGN atom 

tfx_sequent          : tfx_tuple GENTZEN_ARROW tfx_tuple | "(" tfx_sequent ")"

tcf_formula          : tcf_logic_formula | tff_atom_typing
tcf_logic_formula    : tcf_quantified_formula | cnf_formula
tcf_quantified_formula : FOF_QUANTIFIER "[" tff_variable_list "]" ":" cnf_formula

fof_formula          : fof_logic_formula | fof_sequent
fof_logic_formula    : fof_binary_formula | fof_unary_formula | fof_unitary_formula

fof_binary_formula   : fof_binary_nonassoc | fof_binary_assoc

fof_binary_nonassoc  : fof_unit_formula NONASSOC_CONNECTIVE fof_unit_formula

fof_binary_assoc     : fof_or_formula | fof_and_formula
fof_or_formula       : fof_unit_formula VLINE fof_unit_formula | fof_or_formula VLINE fof_unit_formula
fof_and_formula      : fof_unit_formula AND_CONNECTIVE fof_unit_formula | fof_and_formula AND_CONNECTIVE fof_unit_formula
fof_unary_formula    : UNARY_CONNECTIVE fof_unit_formula | fof_infix_unary

fof_infix_unary      : fof_term INFIX_INEQUALITY fof_term

fof_unit_formula     : fof_unitary_formula | fof_unary_formula
fof_unitary_formula  : fof_quantified_formula | fof_atomic_formula | "(" fof_logic_formula ")"

fof_quantified_formula : FOF_QUANTIFIER "[" fof_variable_list "]" ":" fof_unit_formula
fof_variable_list    : VARIABLE | VARIABLE "," fof_variable_list
fof_atomic_formula   : fof_plain_atomic_formula | fof_defined_atomic_formula | fof_system_atomic_formula
fof_plain_atomic_formula : fof_plain_term
//fof_plain_atomic_formula : PROPOSITION | PREDICATE"("fof_arguments")"
fof_defined_atomic_formula : fof_defined_plain_formula | fof_defined_infix_formula
fof_defined_plain_formula : fof_defined_plain_term
//fof_defined_plain_formula : DEFINED_PROPOSITION | DEFINED_PREDICATE"("fof_arguments")"
fof_defined_infix_formula : fof_term INFIX_EQUALITY fof_term

fof_system_atomic_formula : fof_system_term

fof_plain_term       : constant | FUNCTOR "(" fof_arguments ")"

fof_defined_term     : defined_term | fof_defined_atomic_term
fof_defined_atomic_term  : fof_defined_plain_term

fof_defined_plain_term   : defined_constant | DEFINED_FUNCTOR "(" fof_arguments ")"

fof_system_term      : system_constant | SYSTEM_FUNCTOR "(" fof_arguments ")"

fof_arguments        : fof_term | fof_term "," fof_arguments

fof_term             : fof_function_term | VARIABLE
fof_function_term    : fof_plain_term | fof_defined_term | fof_system_term

fof_sequent          : fof_formula_tuple GENTZEN_ARROW fof_formula_tuple | "(" fof_sequent ")"

fof_formula_tuple    : "{}" | "{" fof_formula_tuple_list "}"
fof_formula_tuple_list : fof_logic_formula |  fof_logic_formula "," fof_formula_tuple_list

cnf_formula          : disjunction | "(" disjunction ")"
disjunction          : literal | disjunction VLINE literal
literal              : fof_atomic_formula | UNARY_CONNECTIVE fof_atomic_formula | fof_infix_unary

THF_QUANTIFIER       : FOF_QUANTIFIER | TH0_QUANTIFIER | TH1_QUANTIFIER

TH1_QUANTIFIER       : "!>" | "?*"
TH0_QUANTIFIER       : "^" | "@+" | "@-"

SUBTYPE_SIGN         : "<<"

FOF_QUANTIFIER       : "!" | "?"
NONASSOC_CONNECTIVE  : "<=>" | "=>" | "<=" | "<~>" | "~" VLINE | "~&"
ASSOC_CONNECTIVE     : VLINE | AND_CONNECTIVE
UNARY_CONNECTIVE     : "~"
AND_CONNECTIVE       : "&"

GENTZEN_ARROW        : "-->"
ASSIGNMENT           : ":="

type_constant        : TYPE_FUNCTOR
TYPE_FUNCTOR         : ATOMIC_WORD
DEFINED_TYPE         : ATOMIC_DEFINED_WORD
//DEFINED_TYPE         : "$"otype | "$"o | "$"itype | "$"i | "$"ttype | "$"REAL | "$"rat | "$"int

//SYSTEM_TYPE          : ATOMIC_SYSTEM_WORD

atom                 : untyped_atom | defined_constant
untyped_atom         : constant | system_constant

// PROPOSITION          : PREDICATE
// PREDICATE            : ATOMIC_WORD
// DEFINED_PROPOSITION  : DEFINED_PREDICATE
//DEFINED_PROPOSITION  : "$"true | "$"false
// DEFINED_PREDICATE    : ATOMIC_DEFINED_WORD
//DEFINED_PREDICATE    : "$"distinct | "$"less | "$"lesseq | "$"greater | "$"greatereq | "$"is_int 
//                       | "$"is_rat | "$"box_p | "$"box_i | "$"box_int | "$"box | "$"dia_p 
//                        | "$"dia_i | "$"dia_int | "$"dia

// defined_infix_predの使用部分をINFIX_EQUALITYに置き換えました
defined_infix_pred   : INFIX_EQUALITY
// SYSTEM_PROPOSITION   : SYSTEM_PREDICATE
// SYSTEM_PREDICATE     : ATOMIC_SYSTEM_WORD
INFIX_EQUALITY       : "="
INFIX_INEQUALITY     : "!="

constant             : FUNCTOR
FUNCTOR              : ATOMIC_WORD
system_constant      : SYSTEM_FUNCTOR
SYSTEM_FUNCTOR       : ATOMIC_SYSTEM_WORD
defined_constant     : DEFINED_FUNCTOR
DEFINED_FUNCTOR      : ATOMIC_DEFINED_WORD
//DEFINED_FUNCTOR      : "$"uminus | "$"sum | "$"difference | "$"product | "$"quotient | "$"quotient_e | "$"quotient_t | "$"quotient_f | "$"remainder_e | "$"remainder_t | "$"remainder_f 
//                        | "$"floor | "$"ceiling | "$"truncate | "$"round | "$"to_int | "$"to_rat | "$"to_real
TH1_DEFINED_TERM     : "!!" | "??" | "@@+" | "@@-" | "@="
defined_term         : number | DISTINCT_OBJECT
VARIABLE             : UPPER_WORD

source               : general_term
//source               : dag_source | internal_source | external_source | unknown | [sources]

// sources              : source | source "," sources

// dag_source           : NAME | inference_record
// inference_record     : "inference" "(" inference_rule "," useful_info "," inference_parents ")"
// inference_rule       : ATOMIC_WORD

// inference_parents    : "[]" | "[" parent_list "]"
// parent_list          : parent_info | parent_info "," parent_list
// parent_info          : source parent_details
// parent_details       : ":" general_list | null
// internal_source      : "introduced" "(" INTRO_TYPE optional_info ")"
// INTRO_TYPE           : "definition" | "axiom_of_choice" | "tautology" | "assumption"

// external_source      : file_source | theory | creator_source
// file_source          : "file" "(" FILE_NAME file_info ")"
// file_info            : "," NAME | null
// theory               : "theory" "(" THEORY_NAME optional_info ")"
// THEORY_NAME          : "equality" | "ac"

// creator_source       : "creator" "(" CREATOR_NAME optional_info ")"
// CREATOR_NAME         : ATOMIC_WORD

optional_info        : "," useful_info | null
useful_info          : general_list
//useful_info          : [] | [info_items]
// info_items           : info_item | info_item "," info_items
// info_item            : formula_item | inference_item | general_function

// formula_item         : description_item | iquote_item
// description_item     : "description" "(" ATOMIC_WORD ")"
// iquote_item          : "iquote" "(" ATOMIC_WORD ")"

// inference_item       : inference_status | assumptions_record | new_symbol_record | refutation
// inference_status     : "status" "(" STATUS_VALUE ")" | inference_info

// STATUS_VALUE         : "suc" | "unp" | "sap" | "esa" | "sat" | "fsa" | "thm" | "eqv" | "tac" | "wec" | "eth" | "tau" 
//                         | "wtc" | "wth" | "cax" | "sca" | "tca" | "wca" | "cup" | "csp" | "ecs" | "csa" | "cth" | "ceq" 
//                         | "unc" | "wcc" | "ect" | "fun" | "uns" | "wuc" | "wct" | "scc" | "uca" | "noc"

// inference_info       : "inference_rule" "(" ATOMIC_WORD "," general_list ")"

// assumptions_record   : "assumptions" "(" "[" name_list "]" ")"

// refutation           : "refutation" "(" file_source ")"

// new_symbol_record    : "new_symbols" "(" ATOMIC_WORD "," "[" new_symbol_list "]" ")"
// new_symbol_list      : principal_symbol | principal_symbol "," new_symbol_list

// principal_symbol     : FUNCTOR | VARIABLE

include              : INCLUDE "(" FILE_NAME formula_selection ")" "."
formula_selection    : "," "[" name_list "]" | null
name_list            : NAME | NAME name_list

general_term         : general_data | general_data ":" general_term | general_list
general_data         : ATOMIC_WORD | general_function | VARIABLE | number | DISTINCT_OBJECT | formula_data
general_function     : ATOMIC_WORD "(" general_terms ")"

//general_data         : "bind" "("VARIABLE "," formula_data")" | "bind_type" "("VARIABLE "," bound_type")"
// bound_type           : "$thf(" thf_top_level_type ")" | "$tff(" tff_top_level_type ")"
formula_data         : DOLLAR_THF "(" thf_formula ")" | DOLLAR_TFF "(" tff_formula ")" | DOLLAR_FOF "(" fof_formula ")" | DOLLAR_CNF "(" cnf_formula ")" | DOLLAR_FOT "(" fof_term ")"
general_list         : "[]" | "[" general_terms "]"
general_terms        : general_term | general_term "," general_terms

TPI                  : "tpi"
THF                  : "thf"
TFF                  : "tff"
TCF                  : "tcf"
FOF                  : "fof"
CNF                  : "cnf"
DOLLAR_ITE           : "$ite"
DOLLAR_LET           : "$let"
DOLLAR_THF           : "$thf"
DOLLAR_TFF           : "$tff"
DOLLAR_FOF           : "$fof"
DOLLAR_CNF           : "$cnf"
DOLLAR_FOT           : "$fot"
INCLUDE              : "include"

APPLY_SYMBOL         : "@"

NAME                 : ATOMIC_WORD | INTEGER

ATOMIC_WORD          : LOWER_WORD |  SINGLE_QUOTED

ATOMIC_DEFINED_WORD  : DOLLAR_WORD
ATOMIC_SYSTEM_WORD   : DOLLAR_DOLLAR_WORD
number               : INTEGER | RATIONAL | REAL

FILE_NAME            :  SINGLE_QUOTED
null                 :

COMMENT              : COMMENT_LINE|COMMENT_BLOCK
COMMENT_LINE         : /[%]/ PRINTABLE_CHAR*
COMMENT_BLOCK        : /[\/]//[*]/NOT_STAR_SLASH/[*]//[*]/*/[\/]/
NOT_STAR_SLASH       : "("/[^*]/*/[*]//[*]/*/[^\/*]/")"*/[^*]/*


SINGLE_QUOTED        : SINGLE_QUOTE SQ_CHAR SQ_CHAR * SINGLE_QUOTE

DISTINCT_OBJECT      : DOUBLE_QUOTE DO_CHAR * DOUBLE_QUOTE

DOLLAR_WORD          : DOLLAR LOWER_WORD
DOLLAR_DOLLAR_WORD   : DOLLAR DOLLAR LOWER_WORD
UPPER_WORD           : UPPER_ALPHA ALPHA_NUMERIC *
LOWER_WORD           : LOWER_ALPHA ALPHA_NUMERIC *

VLINE                : /[|]/
STAR                 : /[*]/
PLUS                 : /[+]/
ARROW                : /[>]/
LESS_SIGN            : /[<]/

REAL                 : ( SIGNED_REAL | UNSIGNED_REAL )
SIGNED_REAL          : SIGN UNSIGNED_REAL
UNSIGNED_REAL        : ( DECIMAL_FRACTION | DECIMAL_EXPONENT )
RATIONAL             : ( SIGNED_RATIONAL | UNSIGNED_RATIONAL )
SIGNED_RATIONAL      : SIGN UNSIGNED_RATIONAL
UNSIGNED_RATIONAL    : DECIMAL SLASH POSITIVE_DECIMAL
INTEGER              : ( SIGNED_INTEGER | UNSIGNED_INTEGER )
SIGNED_INTEGER       : SIGN UNSIGNED_INTEGER
UNSIGNED_INTEGER     : DECIMAL
DECIMAL              : ( ZERO_NUMERIC | POSITIVE_DECIMAL )
POSITIVE_DECIMAL     : NON_ZERO_NUMERIC NUMERIC*
DECIMAL_EXPONENT     : ( DECIMAL | DECIMAL_FRACTION ) EXPONENT EXP_INTEGER
DECIMAL_FRACTION     : DECIMAL DOT_DECIMAL
DOT_DECIMAL          : DOT NUMERIC NUMERIC*
EXP_INTEGER          : ( SIGNED_EXP_INTEGER | UNSIGNED_EXP_INTEGER )
SIGNED_EXP_INTEGER   : SIGN UNSIGNED_EXP_INTEGER
UNSIGNED_EXP_INTEGER : NUMERIC NUMERIC*


PERCENTAGE_SIGN      : /[%]/
DOUBLE_QUOTE         : /["]/
DO_CHAR              : ( /[\40-\41\43-\133\135-\176]/ | /[\\]//["\\]/ )
SINGLE_QUOTE         : /[']/

SQ_CHAR              : ( /[\40-\46\50-\133\135-\176]/ | /[\\]//['\\]/ )
SIGN                 : /[+-]/
DOT                  : /[.]/
EXPONENT             : /[Ee]/
SLASH                : /[\/]/
ZERO_NUMERIC         : /[0]/
NON_ZERO_NUMERIC     : /[1-9]/
NUMERIC              : /[0-9]/
LOWER_ALPHA          : /[a-z]/
UPPER_ALPHA          : /[A-Z]/
ALPHA_NUMERIC        : ( LOWER_ALPHA | UPPER_ALPHA | NUMERIC | /[_]/ )
DOLLAR               : /[$]/
PRINTABLE_CHAR       : /./

VIEWABLE_CHAR        : /[.\n]/
